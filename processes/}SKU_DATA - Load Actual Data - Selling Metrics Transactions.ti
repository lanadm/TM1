#region Prolog

#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# Set Variables
#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

strCube_g = 'SKU_DATA';
strWeekCube_g = 't-weeks';
strYearCube_g = 't-yr4fy2';
strLocationCube_g = 't-strctr';
strCategory_g = 'Actual';
strSku_g = '1';
strStat_g = 'Selling Metrics Transactions';
strChannel_g = 'C';

#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# Trim the pWeek Paramater to eliminate leading or trailing blanks
#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

strWeek_g = TRIM(pWeek);

#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# Grab the Fiscal Year from the pWeek Parameter
#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

strFY_g = 'FY ' | SUBST(strWeek_g, 3, 2);

#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# Use the trimmed paramter for week (pWeek), and grab the translation from the t-weeks cube for the appropriate Fiscal Year.
#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

strWeekEle_g = CELLGETS(strWeekCube_g, SUBST(strWeek_g, 5, 2), strFY_g);

#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# Use the value pulled from the t-weeks cube to grab the month and day, then combine with "20" as the first part of the year,
#      then grab the last part of the year from the parameter (pWeek)  
#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

strEndDate_g = SUBST(strWeekEle_g, LONG(strWeekEle_g) - 7, 5) | '/20' | SUBST(strWeekEle_g, LONG(strWeekEle_g) - 1, 2);

#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# Use the end date from above to isolate the Month, Day and Year
#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

strMonth = SUBST(strEndDate_g, 1, 2);
strDay = SUBST(strEndDate_g, 4, 2);
strYear = SUBST(strEndDate_g, 7, 4);

#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# Find the beginning of the week's date and isolate the Month, Day and Year, then set the Start Date.
#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

numEndSerial_g = DAYNO(strYear | '-' | strMonth | '-' | strDay);
numStartSerial_g = numEndSerial_g - 6;

strMonth = SUBST(DATE(numStartSerial_g, 1), 6, 2);
strDay = SUBST(DATE(numStartSerial_g, 1), 9, 2);
strYear = SUBST(DATE(numStartSerial_g, 1), 1, 4);

strStartDate_g = strMonth | '/' | strDay | '/' | strYear;

#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# Set the information variables including directories and names to use for logging and errors
#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

strProcessName = GETPROCESSNAME();
strErrorFile = strProcessName | '_' | TIMST(NOW, '\Y\m\d') | '.csv';
strLogDirectory = CELLGETS('bpmControl','Log Directory','Value');
strLogDirectory = strLogDirectory | IF(SUBST(strLogDirectory, LONG(strLogDirectory), 1) @= '\', '', '\');
strError = strLogDirectory | strErrorFile;

#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# Query data source TM1Data using strStartDate and strEndDate as variables to pull the data
#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

strDataSource_g = 'TM1_CMS';

strQuery_g = 'Select Fiscal_Week_Num_RMS as FiscalWeek, LocationID, Count(Distinct OrderNumber) as NumberOrders ' |
'from CMS2000.dbo.BOHKioskTransactions O with (nolock) join CMS2000.dbo.TimeDimension T with (nolock) ' |
'on T.Phys_Date = O.DrawerDate ' |
'where DrawerDate between ''%strStartDate_g%'' and ''%strEndDate_g%'' and LocationID > 99 ' |
'group by Fiscal_Week_Num_RMS, LocationID ' |
'order by Fiscal_Week_Num_RMS, LocationID ';


# >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> #
# Define Data Source
# >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> #

strQuery = EXPAND(strQuery_g);

DATASOURCETYPE = 'ODBC';
DATASOURCENAMEFORSERVER = strDataSource_g;
DATASOURCENAMEFORCLIENT = strDataSource_g;
DATASOURCEQUERY = strQuery;


#endregion
#region Data

#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
# Grab the Location translation for "GP_Lctn_5-2015" from the t-strctr cube
#--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

strLocation = CELLGETS(strLocationCube_g, TRIM(vLocationID), 'GP_Lctn_5-2015');

IF(STRLOCATION @='');
    ITEMSKIP;
ENDIF;


CELLINCREMENTN(vNumberOrders, strCube_g, strFY_g, strCategory_g, strLocation, strSku_g, strWeekEle_g, strStat_g, strChannel_g);

#endregion
#region Epilog




#endregion